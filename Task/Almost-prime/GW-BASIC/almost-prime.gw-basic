10  'Almost prime
20  FOR K% = 1 TO 5
30   PRINT "k = "; K%; ": ";
40   LET I% = 2
50   LET C% = 0
60   WHILE C% < 10
70    LET AN% = I%: LET AK% = K%: GOSUB 1000
80    IF ISKPRIME <> 0 THEN PRINT USING "### "; I%;: LET C% = C% + 1
90    LET I% = I% + 1
100  WEND
110  PRINT
120 NEXT K%
130 END

995  ' Check if n (AN%) is a k (AK%) prime
1000 LET F% = 0
1010 FOR II% = 2 TO AN%
1020  WHILE AN% MOD II% = 0
1030   IF F% = AK% THEN LET ISKPRIME = 0: RETURN
1040   LET F% = F% + 1
1050   LET AN% = AN% \ II%
1060  WEND
1070 NEXT II%
1080 LET ISKPRIME = (F% = AK%)
1090 RETURN

constant filename = "passwd.txt"
integer fn

constant
 rec1 = {"jsmith","x",1001,1000,{"Joe Smith","Room 1007","(234)555-8917","(234)555-0077","jsmith@rosettacode.org"},"/home/jsmith","/bin/bash"},
 rec2 = {"jdoe","x",1002,1000,{"Jane Doe","Room 1004","(234)555-8914","(234)555-0044","jdoe@rosettacode.org"},"/home/jdoe","/bin/bash"},
 rec3 = {"xyz","x",1003,1000,{"X Yz","Room 1003","(234)555-8913","(234)555-0033","xyz@rosettacode.org"},"/home/xyz","/bin/bash"}

function tostring(sequence record)
    record[3] = sprintf("%d",{record[3]})
    record[4] = sprintf("%d",{record[4]})
    record[5] = join(record[5],",")
    record = join(record,":")
    return record
end function

procedure wait(string what)
    ?sprintf("wait (%s)",{what})
    sleep(1)
    task_yield()
end procedure

    if not file_exists(filename) then
        fn = open(filename,"w")
        if fn!=-1 then -- (someone else just beat us to it?)
            printf(fn,"account:password:UID:GID:fullname,office,extension,homephone,email:directory:shell\n")
            printf(fn,"%s\n",{tostring(rec1)})
            printf(fn,"%s\n",{tostring(rec2)})
            close(fn)
        end if
    end if
    while 1 do
        fn = open(filename,"a")
        if fn!=-1 then exit end if
        wait("append")
    end while
--  ?"file open in append mode"; {} = wait_key()
    while 1 do
        if lock_file(fn,LOCK_EXCLUSIVE,{}) then exit end if
        wait("lock")
    end while
--  ?"file locked"; {} = wait_key()
    printf(fn,"%s\n",{tostring(rec3)})
    unlock_file(fn,{})
    close(fn)
    while 1 do
        fn = open(filename,"r")
        if fn!=-1 then exit end if
        wait("read")
    end while

    ?gets(fn)
    while 1 do
        object line = gets(fn)
        if atom(line) then exit end if
        ?line
        {line} = scanf(line,"%s:%s:%d:%d:%s:%s:%s\n")
        line[5] = split(line[5],',')
        ?line
    end while
    close(fn)

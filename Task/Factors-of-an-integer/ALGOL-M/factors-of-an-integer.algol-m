BEGIN

COMMENT ALGOL-M PROGRAM TO DISPLAY THE FACTORS OF AN INTEGER

INTEGER I, N, LIMIT, FOUND, START, DELTA;
STRING(1) ANOTHER;

COMMENT COMPUTE P MOD Q;
INTEGER FUNCTION MOD (P, Q);
INTEGER P, Q;
BEGIN
    MOD := P - Q * (P / Q);
END;

COMMENT MAIN PROGRAM BEGINS HERE;
ANOTHER := "Y";
WHILE ANOTHER = "Y" OR ANOTHER = "y" DO
  BEGIN
    WRITE ("Number to factor:");
    READ (N);
    WRITE ("The factors are:");

    COMMENT CHECK WHETHER NUMBER IS EVEN OR ODD;
    IF MOD(N, 2) = 0 THEN
      BEGIN
        START := 2;
        DELTA := 1;
      END
    ELSE
      BEGIN
        START := 3;
        DELTA := 2;
      END;

    COMMENT TEST POTENTIAL DIVISORS;
    FOUND := 0;
    I := START;
    LIMIT := N / START;
    WHILE I <= LIMIT DO
      BEGIN
        IF MOD(N, I) = 0 THEN
          BEGIN
            WRITE (I);
            FOUND := FOUND + 1;
          END;
        I := I + DELTA;
        IF FOUND = 0 THEN LIMIT := N / I;
      END;

    IF FOUND = 0 THEN WRITE ("None - the number is prime.");
    WRITE(" ");
    WRITE("Do another (y/n)?");
    READ (ANOTHER);
  END;
WRITE (" ");
WRITE ("Goodbye");
END
